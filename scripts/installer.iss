; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "IRPMon"
#define MyAppVersion "1.0"
#define MyAppPublisher "Martin Dráb"
#define MyAppURL "https://github.com/MartinDrab/IRPMon"
#define MyAppUpdateURL "https://github.com/MartinDrab/IRPMon/releases"
#define ConfigMode "Debug"

#include "path.iss"

[Setup]
AppId={{F913732F-475C-46F8-84AA-80FE454CC7ED}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppUpdateURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
SignedUninstaller=yes
SignedUninstallerDir=..\resources
AppCopyright=Copyright (c) Martin Drab 2015-2020
LicenseFile=..\LICENSE
OutputDir=..\bin
OutputBaseFilename=irpmon-{#MyAppVersion}-setup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64
ChangesEnvironment=true

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Types]
Name: "full"; Description: "Full installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "Runtime"; Description: "Microsoft runtime libraries required by the program"; Types: full custom; Flags: fixed
Name: "Libraries"; Description: "Libraries implementing basic IRPMon functionality"; Types: full custom; Flags: fixed
Name: "CmdLine"; Description: "Command-line interface"; Types: full custom;
Name: "Kernel"; Description: "Kernel driver for monitoring device driver requests"; Types: full custom;
Name: "Application"; Description: "Application for setting up the monitoring, browsing log files and managing IRPMon as a whole"; Types: full custom;
Name: "Server"; Description: "Console application and service for receiving commands accross the network"; Types: full custom;
Name: "Parsers"; Description: "Libraries customizing data parsing mechanisms of the IRPMon application"; Types: full custom;
Name: "Symbols"; Description: "Debug symbols"; Types: full custom;

[Files]
Source: "..\bin\x64\{#ConfigMode}\server\*.exe"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Server;
Source: "..\bin\x64\{#ConfigMode}\server\*.dll"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Server;
Source: "..\bin\x64\{#ConfigMode}\server\*.pdb"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Symbols;
Source: "..\bin\Win32\{#ConfigMode}\server\*.exe"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Server;
Source: "..\bin\Win32\{#ConfigMode}\server\*.dll"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Server;
Source: "..\bin\Win32\{#ConfigMode}\server\*.pdb"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Symbols;

Source: "..\bin\x64\{#ConfigMode}\parser\*.dll"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Server;
Source: "..\bin\x64\{#ConfigMode}\parser\*.pdb"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Symbols;
Source: "..\bin\Win32\{#ConfigMode}\parser\*.dll"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Server;
Source: "..\bin\Win32\{#ConfigMode}\parser\*.pdb"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Symbols;

Source: "..\bin\Win64\{#ConfigMode}\IRPMon.exe"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Application;
Source: "..\bin\Win32\{#ConfigMode}\IRPMon.exe"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Application;

Source: "..\bin\Win64\{#ConfigMode}\*.dll"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: CmdLine;
Source: "..\bin\Win32\{#ConfigMode}\*.dll"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: CmdLine;
Source: "..\bin\x64\{#ConfigMode}\IRPMonc.exe"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: CmdLine;
Source: "..\bin\x64\{#ConfigMode}\*.pdb"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Symbols;
Source: "..\bin\Win32\{#ConfigMode}\IRPMonc.exe"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: CmdLine;
Source: "..\bin\Win32\{#ConfigMode}\*.pdb"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Symbols;

Source: "..\bin\x64\{#ConfigMode}\kernel\*.dll"; DestDir: "{sysnative}\drivers\IRPMon\"; Check: Is64BitInstallMode; Flags: ignoreversion; Components: Kernel;
Source: "..\bin\x64\{#ConfigMode}\kernel\*.sys"; DestDir: "{sysnative}\drivers\IRPMon\"; Check: Is64BitInstallMode; Flags: ignoreversion; Components: Kernel;
Source: "..\bin\x64\{#ConfigMode}\kernel\*.pdb"; DestDir: "{sysnative}\drivers\IRPMon\"; Check: Is64BitInstallMode; Flags: ignoreversion; Components: Symbols;
Source: "..\bin\Win32\{#ConfigMode}\kernel\*.dll"; DestDir: "{sysnative}\drivers\IRPMon\"; Check: not Is64BitInstallMode; Flags: ignoreversion; Components: Kernel;
Source: "..\bin\Win32\{#ConfigMode}\kernel\*.sys"; DestDir: "{sysnative}\drivers\IRPMon\"; Check: not Is64BitInstallMode; Flags: ignoreversion; Components: Kernel;
Source: "..\bin\Win32\{#ConfigMode}\kernel\*.pdb"; DestDir: "{sysnative}\drivers\IRPMon\"; Check: not Is64BitInstallMode; Flags: ignoreversion; Components: Symbols;

Source: "..\bin\x64\{#ConfigMode}\dlls\*.dll"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Libraries;
Source: "..\bin\x64\{#ConfigMode}\dlls\*.pdb"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Symbols;
Source: "..\bin\Win32\{#ConfigMode}\dlls\*.dll"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Libraries;
Source: "..\bin\Win32\{#ConfigMode}\dlls\*.pdb"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Symbols;
Source: "..\resources\ioctl.txt"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Libraries;
Source: "..\resources\ntstatus.txt"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Libraries;
Source: "..\resources\winerr.txt"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Libraries;
Source: "..\resources\ioctl.txt"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Libraries;
Source: "..\resources\ntstatus.txt"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Libraries;
Source: "..\resources\winerr.txt"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Libraries;

Source: "..\dlls\Win32\{#ConfigMode}\*.dll"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Runtime;
Source: "..\dlls\x64\{#ConfigMode}\*.dll"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Runtime;
Source: "..\dlls\Win32\*.dll"; DestDir: "{app}\x86"; Flags: ignoreversion; Components: Runtime;
Source: "..\dlls\x64\*.dll"; DestDir: "{app}\x64"; Flags: ignoreversion; Components: Runtime;

[Tasks]
Name: DriverInstall; Description: "Install IRPMon driver service"; Components: Kernel; GroupDescription: "Monitoring driver";
Name: DriverAuto; Description: "Run IRPMon driver on startup"; Components: Kernel; GroupDescription: "Monitoring driver";
Name: ServerInstall; Description: "Install IRPMon server service"; Components: Server and Kernel; GroupDescription: "Server service";
Name: ServerAuto; Description: "Run IRPMon server on startup"; Components: Server and Kernel; GroupDescription: "Server service";
Name: AppDesktop; Description: "Create Desktop shortcuts"; Components: Application; GroupDescription: "GUI Application";
Name: AppStartMenu; Description: "Create Start Menu shortcuts"; Components: Application; GroupDescription: "GUI Application";
Name: envPath; Description: "Add to PATH variable"; 

[Run]
Filename: "sc.exe"; Parameters: "start IRPMonDrv"; Description: "Start IRPMon driver"; Flags: postinstall shellexec unchecked;
Filename: "sc.exe"; Parameters: "start IRPMonSvc"; Description: "Start tIRPMon server service"; Flags: postinstall shellexec unchecked;
Filename: "{app}\README.md"; Description: "View documentation"; Flags: skipifsilent postinstall shellexec unchecked;

[Icons]
Name: "{commondesktop}\IRPMon 32-Bit"; Tasks: AppDesktop; Filename: "{app}\x86\IRPMon.exe"; Comment: "IRPMon 32-bit"
Name: "{commondesktop}\IRPMon 64-Bit"; Tasks: AppDesktop; Filename: "{app}\x64\IRPMon.exe"; Comment: "IRPMon 64-bit"
Name: "{group}\IRPMon 32-Bit"; Tasks: AppStartMenu; Filename: "{app}\x86\IRPMon.exe"; Comment: "IRPMon 32-bit"
Name: "{group}\IRPMon 64-Bit"; Tasks: AppStartMenu; Filename: "{app}\x64\IRPMon.exe"; Comment: "IRPMon 64-bit"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Tasks: AppStartMenu; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Tasks: AppStartMenu; Filename: "{uninstallexe}"

[Code]
Const
  DriverServiceName = 'irpmndrv';
	DriverServiceDisplayName = 'IRPMon Driver';
  ServerServiceName = 'IRPMonSvc';
	ServerServiceDisplayName = 'IRPMon Server';

Var
  DriverInstall : Boolean;
  DriverAuto : Boolean;
  ServiceInstall : Boolean;
  ServiceAuto : Boolean;
  Succeeded : Boolean;
	ServicesInstalled : Boolean;
	
function ShouldSkipPage(PageID: Integer): Boolean;
begin
Result := False;
If PageID = wpSelectProgramGroup Then
  Result := True;
end;


Const
	ERROR_SERVICE_EXISTS = $431;
  ERROR_SERVICE_ALREADY_RUNNING = $420;
	
	
	SC_MANAGER_CREATE_SERVICE	=	$0002;
  SC_MANAGER_CONNECT = $0001;
	
	SERVICE_DELETE = $10000;
	SERVICE_START = $10;
	SERVICE_STOP = $20;
	
	SERVICE_ERROR_NORMAL = 0;
	
	SERVICE_BOOT_START = 0;
	SERVICE_SYSTEM_START = 1;
	SERVICE_AUTO_START = 2;
	SERVICE_DEMAND_START = 3;
	SERVICE_DISABLED = 4;
	
	SERVICE_KERNEL_DRIVER = $1;
	SERVICE_WIN32_OWN_PROCESS = $10;
	
	SERVICE_CONTROL_STOP = $1;
	
Type
  SC_HANDLE = THandle;
  SERVICE_STATUS = Record
		dwServiceType : cardinal;
    dwCurrentState : cardinal;
    dwControlsAccepted : cardinal;
    dwWin32ExitCode : cardinal;
    dwServiceSpecificExitCode : cardinal;
    dwCheckPoint : cardinal;
    dwWaitHint : cardinal;
    end;
	
Function GetLastError:Cardinal; External 'GetLastError@Kernel32.dll';
Function OpenSCManager(lpMachineName:string; lpDatabaseName:string; dwDesiredAccess:Cardinal):SC_HANDLE; External 'OpenSCManagerW@advapi32.dll';
Function CloseServiceHandle(hSCObject:SC_HANDLE):LongBool; External 'CloseServiceHandle@advapi32.dll';
Function OpenService(hSCManager:SC_HANDLE; lpServiceName:string; dwDesiredAccess:Cardinal):SC_HANDLE; External 'OpenServiceW@advapi32.dll';
Function CreateService(hSCManager:SC_HANDLE; lpServiceName:string; lpDisplayName:string; dwDesiredAccess:Cardinal; dwServiceType:Cardinal; dwStartType:Cardinal; dwErrorControl:Cardinal; lpBinaryPathName:string; lpLoadOrderGroup:string; lpdwTagId:string; lpDependencies:string; lpServiceStartName:string; lpPassword:string):SC_HANDLE; External 'CreateServiceW@advapi32.dll';
Function StartService(hService:SC_HANDLE; dwNumServiceArgs:Cardinal; lpServiceArgVectors:PAnsiChar):LongBool; External 'StartServiceW@advapi32.dll';
Function ControlService(hService:SC_HANDLE; dwControl:Cardinal; Var lpServiceStatus:SERVICE_STATUS):LongBool; External 'ControlService@advapi32.dll';
Function DeleteService(hService:SC_HANDLE):LongBool; External 'DeleteService@advapi32.dll';


Function InstallService(AName:string; ADisplayName:string; AType:Cardinal; AStart:Cardinal; AFileName:string):Cardinal;
Var
	hScm : SC_HANDLE;
	hService : SC_HANDLE;
	fileNamePrefix : string;
begin
Result := 0;
hScm := OpenSCManager('', '', SC_MANAGER_CREATE_SERVICE);
If hScm <> 0 Then
	begin
  If AType <> SERVICE_KERNEL_DRIVER Then
    begin
    fileNamePrefix := ExpandConstant('{app}');
    If IsWin64 Then
      fileNamePrefix := fileNamePrefix + '\x64\'
    Else fileNamePrefix := fileNamePrefix + '\x86\';
    end
	Else fileNamePrefix := 'system32\drivers\IRPMon\';

	AFileName := fileNamePrefix + AFileName;			
	hService := CreateService(hScm, AName, ADisplayName, SERVICE_START, AType, AStart, SERVICE_ERROR_NORMAL, AFileName, '', '', '', '', '');
				
				
	If hService = 0 Then
		begin
		Result := GetLastError;
		If Result = ERROR_SERVICE_EXISTS Then
			begin
			Result := 0;
			hService := OpenService(hScm, AName, SERVICE_START Or SERVICE_DELETE);
			If hService = 0 Then
				Result := GetLastError;
			end;

		If Result = 0 Then
			CloseServiceHandle(hService);
		end;
				
	CloseServiceHandle(hScm);
	end
Else Result := GetLastError;
end;

Function UninstallService(AName:string):Cardinal;
Var
	ss : SERVICE_STATUS;
	hScm : SC_HANDLE;
	hService : SC_HANDLE;
begin
Result := 0;
hScm := OpenSCManager('', '', SC_MANAGER_CONNECT);
If hScm <> 0 Then
	begin
	hService := OpenService(hScm, AName, SERVICE_STOP Or SERVICE_DELETE);
	If hService <> 0 Then
		begin
		If Not ControlService(hService, SERVICE_CONTROL_STOP, ss) Then
			Result := GetLastError;
		
		If Not DeleteService(hService) Then
			Result := GetLastError;
		
		CloseServiceHandle(hService);
		end
	Else Result := GetLastError;
		
	CloseServiceHandle(hScm);
	end
Else Result := GetLastError;
end;

Procedure ErrorMessage(AMsg:string);
begin
MsgBox(AMsg, mbError, MB_OK);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
Var
  err : Cardinal;
	serviceFileName : string;
	serviceStartType : Cardinal;
begin
err := 0;
Result := True;
Case CurPageID Of
  wpSelectTasks : begin  
    DriverInstall := IsTaskSelected('DriverInstall');
    DriverAuto := IsTaskSelected('DriverAuto');
    ServiceInstall := IsTaskSelected('ServerInstall');
    ServiceAuto := IsTaskSelected('ServerAuto');
    end;
  wpReady : begin
		err := 0;
    If DriverInstall Then
      begin
			serviceFileName := 'irpmndrv.sys';
			serviceStartType := SERVICE_DEMAND_START;
			If DriverAuto Then
				serviceStartType := SERVICE_SYSTEM_START;

			err := InstallService(DriverServiceName, DriverServiceDisplayName, SERVICE_KERNEL_DRIVER, serviceStartType, serviceFileName);
			If err <> 0 Then
				ErrorMessage('Unable to install the IRPMon driver: ' + IntToStr(err));
			end;
			
    If (err = 0) Or (ServiceInstall) Then
      begin
			serviceFileName := 'server-svc.exe';
			serviceStartType := SERVICE_DEMAND_START;
			If ServiceAuto Then
				serviceStartType := SERVICE_AUTO_START;

			err := InstallService(ServerServiceName, ServerServiceDisplayName, SERVICE_WIN32_OWN_PROCESS, serviceStartType, serviceFileName);
			If err <> 0 Then
				begin
				If DriverInstall Then
					UninstallService(DriverServiceName);

				ErrorMessage('Unable to install the Server service' + IntToStr(err));
				end;
			end;
			
		servicesInstalled := (err = 0);	
    end;
  end;
	
Result := (err = 0);
end;

Procedure DeinitializeSetup();
begin
If (Not Succeeded) And (servicesInstalled) Then
	begin
	If ServiceInstall Then
		UninstallService(ServerServiceName);
  
	If DriverInstall Then
		UninstallService(DriverServiceName);
	end;
end;

Procedure CurStepChanged(CurStep: TSetupStep);
Var
  p : String;
begin
If (CurStep = ssPostInstall) And (IsTaskSelected('envPath')) Then
  begin
  p := ExpandConstant('{app}');
  If IsWin64 Then
    p := p + '\x64'
   Else p := p + '\x86';

  EnvAddPath(p);
  end;

Succeeded := (CurStep = ssDone);
end;

Procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
Var
  p : String;
begin
If CurUninstallStep = usPostUninstall Then
  begin
  p := ExpandConstant('{app}');
  If IsWin64 Then
    p := p + '\x64'
   Else p := p + '\x86';

  EnvRemovePath(p);
  end;
end;

Procedure InitializeUninstallProgressForm();
begin
UninstallService(ServerServiceName);
UninstallService(DriverServiceName);
end;
